import se.bjurr.gitchangelog.api.GitChangelogApi

import static se.bjurr.gitchangelog.api.GitChangelogApi.gitChangelogApiBuilder

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.2'
    id "com.diffplug.gradle.oomph.ide" version "3.17.1"
    id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.55"
    id "com.fizzed.rocker" version "0.24.0"
//    id 'com.zyxist.chainsaw' version '0.3.1'
}

defaultTasks 'clean', 'shadow'

buildDir = file('dist')

apply plugin: 'java'

group = 'org.cfmlprojects'
description = 'RunWar Gradle build script'
archivesBaseName = 'runwar'
apply from: 'gradle/config.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    //options.bootstrapClasspath = fileTree(include: ['**/jce.jar','**/rt.jar'], dir: "${System.properties['java.home']}/")
    options.encoding = 'UTF-8'
    options.compilerArgs += '-Xlint:unchecked'
    options.compilerArgs += '-Xlint:deprecation'
}

wrapper{
    distributionType = Wrapper.DistributionType.ALL
    doLast{
        // customise gradlew for our JVM auto-download script
        def javaRelease = System.getenv( 'JVM_RELEASE' ) ?: 'latest'
        def javaMajorVersion = System.getenv( 'JVM_MAJOR_VERSION' ) ?: 8
        def javaDir = System.getenv( 'JVM_HOME' ) ?: "gradle/jvm/${javaMajorVersion}/latest"
        def envVars = "\nexport JVM_MAJOR_VERSION=${ javaMajorVersion }\nexport JVM_DIR=\"${ javaDir }\"\nexport JVM_RELEASE=${ javaRelease }\nexport GRADLE_USER_HOME=\"\${HOME}/.gradle\"\n"
        envVars += ". \"\${APP_HOME}/gradle/jvm-setup.sh\"\n"
        scriptFile.text = new StringBuilder( scriptFile.text ).insert( scriptFile.text.indexOf( 'APP_NAME' ) - 1, envVars )
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
//    options.addBooleanOption('html5', true)
}

tasks.withType(Test) {
}

sourceSets {
    main {
        output.dir(generatedResources, builtBy: 'generateVersionFile')
        rocker {
            srcDir('src/main/rocker')
        }
    }
}

/*
 processResources {
 filesMatching('properties/*.properties') {
 filter ReplaceTokens, tokens: [
 'build.version': project.property("version"),
 'build.timestamp': project.buildTimestamp
 ]F
 }
 }
 */

test {
    // Enable JUnit 5 (Gradle 4.6+).
    useJUnitPlatform { /*jvmArgs "-verbose:class"*/ }

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging { events "passed", "skipped", "failed" }

    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true
}

rocker {
    failOnError true
    skipTouch true
    // must not be empty when skipTouch is equal to false
    touchFile ""
    javaVersion '1.8'
    extendsClass null
    extendsModelClass null
    optimize null
    discardLogicWhitespace true
    targetCharset null
    suffixRegex null
    postProcessing null
}

// create an OSGI manifest
apply plugin: 'com.diffplug.gradle.osgi.bndmanifest'
osgiBndManifest { copyTo 'META-INF/MANIFEST.MF' }

jar {
    archiveClassifier = 'core'
    manifest.attributes(
            'Main-Class': 'runwar.Start',
            'Can-Redefine-Classes': 'true',
            'Automatic-Module-Name': 'runwar',
            'Can-Retransform-Classes': 'true',
            'Implementation-Version': project.version,
            '-exportcontents': 'runwar.*',
            '-removeheaders': 'Bnd-LastModified,Bundle-Name,Created-By,Tool,Private-Package,Require-Capability',
            'Import-Package': '!javax.annotation.*,*',
            'Bundle-SymbolicName': project.name,
            'Bundle-RequiredExecutionEnvironment': 'JavaSE-1.8',
            'Require-Capability': 'osgi.ee;filter:="(&(osgi.ee=JavaSE)(version=1.8))"',
            'Bundle-Vendor': 'cfmlprojects.org',
            'Bundle-License': "MIT"
    )
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

/*
tasks.withType(JavaCompile) {
    options.compilerArgs.addAll([
            "--add-exports",
            "java.base/jdk.internal.misc=ALL-UNNAMED",
            "--add-exports",
            "java.base/jdk.internal.misc=runwar"
    ])
}
*/

shadowJar {
    archiveClassifier = null
    mergeServiceFiles()
    exclude('**/*.java')
}
apply from: 'gradle/proguard.gradle'
apply from: 'gradle/maven.gradle'

task copyToLib(type: Copy) {
    doFirst { delete "$buildDir/libs" }
    into "$buildDir/libs"
    from configurations.runtime
}

/*
 task generateGitChangelogTemplateWithGitHubIssues(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
 gitHubApi = "https://api.github.com/repos/Ortus-Solutions/runwar"
 gitHubToken = System.properties['GITHUB_OAUTH2TOKEN']
 gitHubIssuePattern = "#([0-9]*)"
 file = new File("${buildDir}/CHANGELOG.md")
 templateContent = file('gradle/changelog/changelog.mustache').getText('UTF-8')
 }
 */

task gitChangelogTask() {
    doLast {
        GitChangelogApi builder;
        builder = gitChangelogApiBuilder()
        /*
         println builder.withGitHubApi("https://api.github.com/repos/Ortus-Solutions/runwar")
         .withGitHubToken(System.properties['GITHUB_OAUTH2TOKEN'])
         .withGitHubIssuePattern("#([0-9]*)")
         .withTemplatePath('gradle/changelog/changelog.mustache')
         .render();
         */
        println builder.withFromRef("master")
                .withToRef("HEAD")
                .withGitHubApi("https://api.github.com/repos/Ortus-Solutions/runwar")
                .withRemoveIssueFromMessageArgument(false)
                .withUntaggedName("Untagged version")
                .withNoIssueName("These commits have no issue in their commit comment")
                .withTemplateContent("""
					# Changelog
					
					{{#tags}}
					## {{name}}
					 {{^hasIssue}}
					 GitHub Issues:
					 {{/hasIssue}}
					 {{#issues}}
					  {{#hasLink}}
					  [{{issue}}]({{link}}) {{title}}
					  {{#commits}}
					   [{{hash}}] {{{messageTitle}}} (https://github.com/{{ownerName}}/{{repoName}}/commit/{{hash}}) 
					  {{/commits}}
					  {{/hasLink}}
					 {{/issues}}
					
					 {{#issues}}
					  {{^hasLink}}
					 Commits:
					  {{#commits}}
					  [{{hash}}] {{{messageTitle}}} (https://github.com/{{ownerName}}/{{repoName}}/commit/{{hash}}) 
					  {{/commits}}
					  {{/hasLink}}
					 {{/issues}}
					 
					{{/tags}}
                """.stripIndent())
                .render()

        /*
         println builder.withFromRef("refs/tags/3.7.0")
         .withToRef("refs/tags/3.8.0")
         .withTemplatePath('gradle/changelog/changelog.mustache')
         .render()
         */
        //    file = new File("${buildDir}/CHANGELOG.md");
        //    templateContent = file('gradle/changelog/changelog.mustache').getText('UTF-8');
    }
}

task listUsedClasses() {
    doFirst {
        def verboseFilePath = file('/workspace/runwar/used.txt')
        TreeSet<String> usedJars = new TreeSet<>()
        TreeSet<String> usedClasses = new TreeSet<>(); TreeSet<String> duplicateEntries = new TreeSet<>()
        HashMap<String, String> mergedEntries = new HashMap<>()

        usedJars.clear()
        usedClasses.clear()
        verboseFilePath.eachLine { line ->
            int pos = line.indexOf("from file")
            if (pos < 0)
                return
            String jarName = line.substring(pos + 11, line.length() - 1)
            if (jarName.contains("/jre") || jarName.contains("\\jre") || !jarName.endsWith(".jar"))
                return
            usedJars.add(jarName)
            String className = line.substring(8, pos - 1)
            usedClasses.add(className)
        }
        usedJars.each { println "used jar ${it}" }
        usedClasses.each { println "keep 'class ${it}'" }
    }

}
